description = 'Checkmarx CxSAST Dynamic Engine App'

dependencies {
    implementation project(':cx-dyn-engines-core')
    if (project.hasProperty("aws")) {
        implementation project(':cx-dyn-engines-aws')
    }
    if (project.hasProperty("vmware")) {
        implementation project(':cx-dyn-engines-vmware')
    }
}

jar {
    enabled = false
}

bootJar {
    enabled = true
    manifest {
        attributes("Implementation-Title": "Checkmarx CxSAST Dynamic Engine App",
        "Implementation-Version": version)
    }
}

bootRun {
    enabled = true
    standardInput = System.in

    // required for passing JVM system properties via the gradle command line
    systemProperties = System.properties

    def currentProfile = System.getProperty("spring.profiles.active", activeProfile)
    systemProperty "spring.profiles.active", currentProfile
    println 'bootRun: activeProfile=' + currentProfile
}

test {
    // required for passing JVM system properties via the gradle command line
    systemProperties = System.properties
    systemProperty "spring.profiles.active", System.getProperty("spring.profiles.active") ?: "test," + activeProfile
    println 'test: activeProfile=' + activeProfile
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

springBoot {
    buildInfo()
}
